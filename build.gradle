group 'de.zalando.aruha'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'nakadi-plugin-api'
                packaging 'jar'
                description 'Nakadi API to write extensions'
                url 'https://github.com/zalando/nakadi-plugin-api'

                scm {
                    url 'scm:git@github.com:zalando/nakadi-plugin-api.git'
                    connection 'scm:git@github.com:zalando/nakadi-plugin-api.git'
                    developerConnection 'scm:git@github.com:zalando/nakadi-plugin-api.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'v1ctor'
                        name 'Victor Buldakov'
                    }
                }
            }
        }
    }
}
